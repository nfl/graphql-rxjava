apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
version = '0.0.1'
group = 'com.graphql-java'


repositories {
    maven { url  "http://dl.bintray.com/andimarek/graphql-java" }
    mavenCentral()
}

apply plugin: 'groovy'

jar {
    from "LICENSE"
}

dependencies {
    compile 'org.antlr:antlr4-runtime:4.5.1'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.graphql-java:graphql-java:3.0.0'
    compile 'io.reactivex:rxjava:1.0.12'
    compile 'org.apache.commons:commons-lang3:3.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.4'
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile 'org.objenesis:objenesis:2.1'
}

task sourcesJar(type: Jar) {
    dependsOn classes
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'graphql-rxjava'
                    description 'GraphQL Rx-Java'
                    url "https://github.com/nfl/graphql-rxjava"
                    scm {
                        url "https://github.com/nfl/graphql-rxjava"
                        connection "https://github.com/nfl/graphql-rxjava"
                        developerConnection "https://github.com/nfl/graphql-rxjava"
                    }
                    licenses {
                        license {
                            name 'MIT'
                            url 'https://github.com/nfl/graphql-rxjava/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'tberman'
                            name 'Todd Berman'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
    distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
